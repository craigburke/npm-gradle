
buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.gradle.publish:plugin-publish-plugin:0.9.4'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    }
}

group = 'com.craigburke.gradle'
version = '1.4.1-SNAPSHOT'

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}
ext {
    githubUrl = 'https://github.com/craigburke/client-dependencies-gradle'
}

subprojects{
    apply plugin: 'groovy'
    apply plugin: 'maven-publish'
    apply plugin: 'idea'
    apply plugin: 'com.gradle.plugin-publish'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'codenarc'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7
    
    project.group = rootProject.group
    project.version = rootProject.version

    repositories {
        jcenter()
    }

    publishing {
        publications {
            maven(MavenPublication) {
                pom.withXml {
                    asNode().children().last() + {
                        resolveStrategy = Closure.DELEGATE_FIRST
//                        name project.artifactId
                        description project.description
                        url project.githubUrl
                        scm {
                            url project.githubUrl
                            connection "scm:${project.githubUrl}.git"
                            developerConnection "scm:${project.githubUrl}.git"
                        }
                        licenses {
                            license {
                                name 'The Apache Software License, Version 2.0'
                                url 'http://www.apache.org/license/LICENSE-2.0.txt'
                                distribution 'repo'
                            }
                        }
                        developers {
                            developer {
                                id 'craigburke'
                                name 'Craig Burke'
                                email 'craig@craigburke.com'
                            }
                        }
                    }
                }
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }
    codenarc {
        toolVersion = '0.25.1'
        configFile = file("${rootProject.projectDir}/config/codenarc/rules.groovy")
    }
    bintray {
        user = project.hasProperty('bintrayUsername') ? project.bintrayUsername : ''
        key = project.hasProperty('bintrayApiKey') ? project.bintrayApiKey : ''
        publications = ['maven']
        publish = true

        pkg {
            repo = 'gradle'
            userOrg = 'craigburke'
            githubRepo = 'craigburke/client-dependencies-gradle'
            githubReleaseNotesFile = 'README.adoc'
            version {
                mavenCentralSync {
                    user = project.hasProperty('mavenCentralUsername') ? project.mavenCentralUsername : ''
                    password = project.hasProperty('mavenCentralPassword') ? project.mavenCentralPassword : ''
                }
            }
        }
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from 'build/docs/javadoc'
    }

    pluginBundle {
        website = project.githubUrl
        vcsUrl = project.githubUrl
        description = 'Install client side dependencies from NPM or Bower by declaring dependencies in build.gradle.'
        tags = ['npm', 'bower', 'client', 'web']

        plugins {
            clientDependencies {
                id = 'com.craigburke.client-dependencies'
                displayName = 'Client Dependencies Plugin'
            }
        }
    }



    task('release', dependsOn: ['bintrayUpload', 'publishPlugins'])

}



